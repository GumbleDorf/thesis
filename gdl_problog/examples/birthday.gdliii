(role albert) (role bernard) (role cheryl)
(date may 15) (date may 16) (date may 19)
(date jun 17) (date jun 18) (date jul 14) (date jul 16)
(date aug 14) (date aug 15) (date aug 17)
(succ 0 1) (succ 1 2) (succ 2 3) (succ 3 4) (init (step 0))
(<= (legal cheryl (choose ?m ?d)) (true (step 0)) (date ?m ?d))
(<= (sees albert ?m) (does cheryl (choose ?m ?d)))
(<= (sees bernard ?d) (does cheryl (choose ?m ?d)))
(<= (next (secret ?m ?d)) (does cheryl (choose ?m ?d)))
(<= (next (secret ?m ?d)) (true (secret ?m ?d)))
(<= (next (step ?n)) (true (step ?m)) (succ ?m ?n))
(<= (legal albert noop) (or (true (step 0)) (true (step 2))))
(<= (legal bernard noop) (not (true (step 2))))
(<= (legal cheryl noop) (not (true (step 0))))
(<= (birthday ?m ?d) (true (secret ?m ?d)))
(<= (knowsDate ?r) (knows ?r (birthday ?m ?d)))
(<= (notKnowsDate ?r)(not (knowsDate ?r)))
(<= (legal albert sayUnknown)
(true (step 1)) (not (knowsDate albert))
(knows albert (notKnowsDate bernard)))
(<= (legal bernard sayKnown)
(true (step 2)) (knowsDate bernard))
(<= (legal albert sayKnown)
(true (step 3)) (knowsDate albert))
(<= (sees ?r ?m)
(role ?r)
(or (does albert ?m) (does bernard ?m)))
(<= terminal (true (step 4)))